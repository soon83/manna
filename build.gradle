buildscript {
    ext {
        springBootVersion = '2.7.1'
        springCloudVersion = '2021.0.1'
        boot = 'org.springframework.boot'
        lombok = 'org.projectlombok:lombok'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "$boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

allprojects {
    group = 'com.sss'
    version = '0.0.1'
}

subprojects {
    apply plugin: boot
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'idea'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly lombok
        annotationProcessor lombok
        testImplementation "$boot:spring-boot-starter-test"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

['common'].each {
    def subProjectDir = new File(projectDir, it)
    subProjectDir.eachDir { dir ->
        def projectName = ":${it}-${dir.name}"
        project(projectName) {
            bootJar.enabled(false)
            jar.enabled(true)
        }
    }
}

['server', 'test'].each {
    def subProjectDir = new File(projectDir, it)
    subProjectDir.eachDir { dir ->
        def projectName = ":${it}-${dir.name}"
        project(projectName) {
        }
    }
}
